" ===================== Plugins ============================

call plug#begin()

" User defined Plugins
Plug 'ervandew/supertab'         " Tab completion
Plug 'gerw/vim-HiLinkTrace'      " :HLT! to show highlighting trace
Plug 'godlygeek/tabular'         " :Tab to align given format
Plug 'jiangmiao/auto-pairs'      " Autoclose quotes and other delimiters
Plug 'jistr/vim-nerdtree-tabs'   " Makes NERDTree work with tabs
Plug 'matchit.zip'			     " Improved % matching
Plug 'neomake/neomake'           " Syntax checking
Plug 'scrooloose/nerdtree'       " \t for filesystem tree
Plug 'sickill/vim-pasta'         " Adjust pasting to destination context
Plug 'tomtom/tcomment_vim'       " gc to comment
Plug 'tpope/vim-abolish'         " :S to do smart substitute (understands capitalization)
Plug 'tpope/vim-sleuth'          " Autodetect tabbing
Plug 'tpope/vim-surround'        " cs to change surrounding delimiters
Plug 'yggdroot/indentline'       " Indentation guide lines

" Languages
Plug 'sheerun/vim-polyglot'
Plug 'joshglendenning/vim-caddyfile'
Plug 'posva/vim-vue'
Plug 'gutenye/json5.vim'

" Colors
Plug 'joshglendenning/vim-darcula-colors'

call plug#end()

" ================ Plugin Settings ===================

let g:indentLine_char = '|'
let g:nerdtree_tabs_focus_on_files = 1
let g:nerdtree_tabs_open_on_console_startup = 1

" ================ General Config ====================

set number                      " Line numbers are good
set numberwidth=4				" Use up to four digits
set showcmd                     " Show incomplete cmds down the bottom
set showmode                    " Show current mode down the bottom
set visualbell                  " No sounds
set modelines=5					" Enables modelines

"Reload files changed outside vim, but prompt if changed locally
au CursorHold,CursorHoldI,FileChangedShell * silent! checktime

" This makes vim act like all other editors, buffers can
" exist in the background without being in a window.
" http://items.sjbach.com/319/configuring-vim-right
set hidden

" Switch to existing tab if buffer is open, creating new one if not
set switchbuf=usetab,newtab

" Turn on filetype plugins (and indent)
filetype on
filetype plugin on
filetype indent on

" Theme
colors darcula

" Highlights characters past column 80
highlight OverLength ctermbg=darkgray
match OverLength /\%81v.\+/

" Display tabs and trailing spaces visually with middle dot char
" set list listchars=tab:\ \ ,trail:Â·

" Turn off swap files
set noswapfile
set nobackup
set nowb

" Keep undo history across sessions, by storing in file.
silent !mkdir ~/.vim/backups > /dev/null 2>&1
set undodir=~/.vim/backups
set undofile

" Scrolling
set sidescroll=1

" Start scrolling when we're this many lines away from margins
set scrolloff=3
set sidescrolloff=15

" ================ Search  ==========================

set viminfo='100,f1  "Save up to 100 marks, enable capital marks
set ignorecase
set smartcase

" ================ Indentation ======================

set smartindent

" Use \t with a width of 4 by default
set noexpandtab
set shiftwidth=4
set tabstop=4

function SetTab(...)
	if a:0 == 0
		set noexpandtab
		set shiftwidth=4
		set tabstop=4
	else
		set expandtab
		let &expandtab=a:1
		let &shiftwidth=a:1
	endif
endfunction

set cinoptions=(0,Ws,m1,J1,j1

" ================= Line Wrapping ===================

" Allow toggling between wrapping and not wrapping
function WrapToggle()
	if &wrap
		set nowrap
	else
		set wrap
	endif
endfunction
" Mapped to \w
map <Leader>w mz:execute WrapToggle()<CR>`z

" Wrap lines at convenient points
set linebreak

" ================ Folds ============================

" Fold manually, set to indent for indent
set foldmethod=manual

" Don't fold by default
set nofoldenable

" ================ Completion =======================

set wildmode=list:longest
set wildmenu                "enable ctrl-n and ctrl-p to scroll thru matches
set wildignore=*.o,*.obj,*~ "stuff to ignore when tab completing
set wildignore+=*vim/backups*
set wildignore+=*sass-cache*
set wildignore+=*DS_Store*
set wildignore+=vendor/rails/**
set wildignore+=vendor/cache/**
set wildignore+=*.gem
set wildignore+=log/**
set wildignore+=tmp/**
set wildignore+=*.png,*.jpg,*.gif

" ================ Fixes ============================

" vp doesn't replace paste buffer
function! RestoreRegister()
	let @" = s:restore_reg
	return ''
endfunction
function! s:Repl()
	let s:restore_reg = @"
	return "p@=RestoreRegister()\<cr>"
endfunction
vmap <silent> <expr> p <sid>Repl()

" Front-matter syntax highlighting for md and html
autocmd Filetype html,markdown,liquid syntax match Comment /\%^---\_.\{-}---$/
autocmd Filetype html,markdown,liquid syntax match Comment /\_.\{-}---$/

" Properly indent c++11 lambdas
" autocmd FileType cpp setlocal cindent cino=j1,(0,ws,Ws

" ================ Mappings ==========================

" Window moving shortcuts
nmap <silent> gk :wincmd k<CR>
nmap <silent> gj :wincmd j<CR>
nmap <silent> gh :wincmd h<CR>
nmap <silent> gl :wincmd l<CR>

" \t to open terminal
noremap <silent> <Leader>t :botright 20split<CR>:term<CR>

" \d to toggle NERDTree
noremap <silent> <Leader>d :NERDTreeTabsToggle<CR>

" \f to find file in NERDTree
noremap <silent> <Leader>f :NERDTreeTabsFind<CR>

" :w!! to sudo write
cmap w!! w !sudo tee % >/dev/null

" :W is :w
cnoreabbrev <expr> W ((getcmdtype() is# ':' && getcmdline() is# 'W')?('w'):('W'))

" J to join line
vnoremap J :left<CR>gv:join!<CR>gv:s/,/, /ge<CR>gv=gv:s/\s\+$//e<CR>

" K to split line
vnoremap K <ESC>o<ESC>mkgv:s/\([({\[,]\)/\1\r/ge<CR>`<v`k:s/\([)}\]]\)/\r\1/ge<CR>`<v`k=`kddk

" Escape twice to exit terminal mode
tnoremap <Esc><Esc> <C-\><C-n>

" \c clears the quickfix list
noremap <Leader>c :call setqflist([])<CR>

" ===================== Project Settings ===============
set exrc
set secure
